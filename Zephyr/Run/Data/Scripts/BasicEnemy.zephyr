StateMachine
{
	Vec2 position;		
	Vec2 targetPosition = (5,5);
	String targetId = "player";
	Number health;

	// -----------------------------------------------------------------------------------------
	// Global events
	// -----------------------------------------------------------------------------------------
	// TODO: reserve Spawn and Die as special events that the user can't fire 
	Number maxHealth;
	OnEvent( Spawn )
	{
		health = maxHealth;
		FireEvent( EnemySpawned() );
	}

	Number newHealth;
	OnEvent( HealthUpdated )
	{
		health = newHealth;
	}

	OnEvent( Die )
	{
		FireEvent( EnemyDied() );
	}

	// TODO: Make this not terrible
	Vec2 newPos;
	OnEvent( UpdateEntityPosition )
	{
		position = newPos;
	}

	OnEvent( UpdateTargetPosition )
	{
		targetPosition = newPos;
	}

	FireEvent( GetNewWanderTargetPosition() );

	// -----------------------------------------------------------------------------------------
	// Wander
	// -----------------------------------------------------------------------------------------
	State Wander
	{	
		OnEvent( TestEvent )
		{		
			FireEvent( GetNewWanderTargetPosition() );
		}

		OnEvent( TargetFound )
		{		
			if( health == 1 )
			{
				ChangeState( Flee );
			}
			else
			{
				ChangeState( Chase );
			}
		}

		FireEvent( PrintDebugText( text = "Wander" ) );
		
		FireEvent( CheckForTarget( id = targetId, maxDist = 3 ) );
		FireEvent( MoveToLocation( pos=targetPosition ) );

		if( position == targetPosition )
		{
			FireEvent( GetNewWanderTargetPosition() );
		}
	}

	// -----------------------------------------------------------------------------------------
	// Chase
	// -----------------------------------------------------------------------------------------
	State Chase
	{
		OnEvent( TestEvent )
		{		
			FireEvent( GetNewWanderTargetPosition() );
			ChangeState( Wander );
		}
		
		Number distance;
		OnEvent( UpdateDistanceToTarget )
		{		
			if( distance > 3 )
			{
				ChangeState( Wander );
			}
		}

		FireEvent( PrintDebugText( text = "Chase" ) );

		if( health == 1 )
		{
			ChangeState( Flee );
		}

		FireEvent( GetDistanceToTarget( id = targetId ) );

		FireEvent( ChaseTargetEntity( id = targetId ) );
	}

	// -----------------------------------------------------------------------------------------
	// Flee
	// -----------------------------------------------------------------------------------------
	State Flee
	{
		OnEvent( TestEvent )
		{		
			FireEvent( GetNewWanderTargetPosition() );
			ChangeState( Wander );
		}
		
		Number distance;
		OnEvent( UpdateDistanceToTarget )
		{		
			if( distance > 5 )
			{
				ChangeState( Wander );
			}
		}

		FireEvent( PrintDebugText( text = "Flee" ) );

		FireEvent( GetDistanceToTarget( id = targetId ) );

		FireEvent( FleeTargetEntity( id = targetId ) );
	}
}

