StateMachine
{
	Vec2 position;		
	Vec2 targetPosition;
	String targetId = "player";
	Number health;

	Vec2 lastPosition;
	Number numFramesInSameSpace;
	
	
	// -----------------------------------------------------------------------------------------
	// Global events
	// -----------------------------------------------------------------------------------------
	// TODO: reserve Spawn and Die as special events that the user can't fire 
	Number maxHealth;
	OnEvent( Spawn )
	{
		health = maxHealth;
		FireEvent( EnemySpawned() );
		FireEvent( GetNewWanderTargetPosition() );
	}

	Number newHealth;
	OnEvent( HealthUpdated )
	{
		health = newHealth;
	}

	OnEvent( Die )
	{
		FireEvent( EnemyDied() );
	}

	// TODO: Make this more intuitive
	Vec2 newPos;
	OnEvent( UpdateEntityPosition )
	{
		lastPosition = position;
		position = newPos;
	}

	OnEvent( UpdateTargetPosition )
	{
		targetPosition = newPos;
	}
	
	// -----------------------------------------------------------------------------------------
	// Wander
	// -----------------------------------------------------------------------------------------
	State Wander
	{	
		OnEvent( TargetFound )
		{		
			ChangeState( Chase );
		}
		
		FireEvent( PrintDebugText( text = "Wander" ) );
		
		//FireEvent( CheckForTarget( id = targetId, maxDist = 3 ) );
		FireEvent( MoveToLocation( pos=targetPosition ) );

		if( position == targetPosition )
		{
			FireEvent( GetNewWanderTargetPosition() );
		}

		// Check for stuck on wall, find new position if stuck for a second
		if( lastPosition == position )
		{
			numFramesInSameSpace = numFramesInSameSpace + 1;

			if( numFramesInSameSpace > 60 )
			{
				FireEvent( GetNewWanderTargetPosition() );
			}
		}
		else
		{
			numFramesInSameSpace = 0;
		}
	}

	// -----------------------------------------------------------------------------------------
	// Chase
	// -----------------------------------------------------------------------------------------
	State Chase
	{
		Number distance;
		OnEvent( UpdateDistanceToTarget )
		{		
			if( distance > 3 )
			{
				FireEvent( GetNewWanderTargetPosition() );
				ChangeState( Wander );
			}
		}

		if( health == 1 )
		{
			//ChangeState( Flee );
		}

		FireEvent( PrintDebugText( text = "Chase" ) );

		FireEvent( GetDistanceToTarget( id = targetId ) );

		FireEvent( ChaseTargetEntity( id = targetId ) );
	}

	// -----------------------------------------------------------------------------------------
	// Flee
	// -----------------------------------------------------------------------------------------
	State Flee
	{
		Number distance;
		OnEvent( UpdateDistanceToTarget )
		{		
			if( distance > 5 )
			{
				FireEvent( GetNewWanderTargetPosition() );
				ChangeState( Wander );
			}
		}

		FireEvent( PrintDebugText( text = "Flee" ) );

		FireEvent( GetDistanceToTarget( id = targetId ) );

		FireEvent( FleeTargetEntity( id = targetId ) );
	}
}
