Vec2 position
Vec2 targetPosition
String targetName = "player"
Number health

Vec2 lastPosition 
Number numFramesInSameSpace

Bool active = true
// -----------------------------------------------------------------------------------------
// Global events
// -----------------------------------------------------------------------------------------
// TODO: reserve Spawn and Die as special events that the user can't fire 
Number maxHealth
String mapName
OnEvent( Spawned )
{
	health = maxHealth
	EnemySpawned( mapName = mapName )
	GetNewWanderTargetPosition()
}

Number newHealth
OnEvent( HealthUpdated )
{
	health = newHealth

	if( health == 1 )
	{
		ChangeSpriteAnimation( newAnim = "LowHealth" )
	}
}

OnEvent( Died )
{
	EnemyDied( mapName = mapName )
}

// TODO: Make this more intuitive
Vec2 newPos
OnEvent( PositionUpdated )
{
	lastPosition = position
	position = newPos
}

OnEvent( TargetPositionUpdated )
{
	targetPosition = newPos
}

// Damage
Number attackDamage = 1
Number otherEntityId = -1
OnEvent( CollisionEntered )
{
	if( otherEntityId != -1 )
	{
		DamageEntity( targetId = otherEntityId, damage = attackDamage, damageType = "normal" )
	}
}

// -----------------------------------------------------------------------------------------
// Wander
// -----------------------------------------------------------------------------------------
State Wander
{	
	OnEvent( TargetFound )
	{		
		ChangeState( Chase )
	}
	
	// FireEvent( PrintDebugText( text = "Wander" ) );
	
	OnUpdate()
	{
		CheckForTarget( targetName = targetName, maxDist = 3 )
		MoveToLocation( pos = targetPosition )

		if( position == targetPosition )
		{
			GetNewWanderTargetPosition()
		}

		// Check for stuck on wall, find new position if stuck for a second
		if( lastPosition == position )
		{
			numFramesInSameSpace = numFramesInSameSpace + 1

			if( numFramesInSameSpace > 60 )
			{
				GetNewWanderTargetPosition()
			}
		}
		else
		{
			numFramesInSameSpace = 0
		}
	}
}

// -----------------------------------------------------------------------------------------
// Chase
// -----------------------------------------------------------------------------------------
State Chase
{
	Number distance;
	OnEvent( DistanceToTargetUpdated )
	{		
		if( distance > 3 )
		{
			GetNewWanderTargetPosition()
			ChangeState( Wander )
		}
	}

	OnUpdate()
	{
		if( health == 1 )
		{
			ChangeState( Flee )
		}
		
		//FireEvent( PrintDebugText( text = "Chase" ) );

		GetDistanceToTarget( targetName = targetName )
		ChaseTargetEntity( targetName = targetName )
	}
}

// -----------------------------------------------------------------------------------------
// Flee
// -----------------------------------------------------------------------------------------
State Flee
{
	Number distance;
	OnEvent( DistanceToTargetUpdated )
	{		
		if( distance > 5 )
		{
			GetNewWanderTargetPosition()
			ChangeState( Wander )
		}
	}

	//FireEvent( PrintDebugText( text = "Flee" ) );
	OnUpdate()
	{
		GetDistanceToTarget( targetName = targetName ) 
		FleeTargetEntity( targetName = targetName ) 
	}
}
