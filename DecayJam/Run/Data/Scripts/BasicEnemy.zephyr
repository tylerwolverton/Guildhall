StateMachine
{
	Number id;
    Number attackTargetId = -1;
	Vec2 position;		
	Vec2 targetPosition;
	String targetName = "player";
	Number health;

	Vec2 lastPosition;
	Number numFramesInSameSpace;
	
	Bool active = true;
	// -----------------------------------------------------------------------------------------
	// Global events
	// -----------------------------------------------------------------------------------------
	// TODO: reserve Spawn and Die as special events that the user can't fire 
	Number entityId;
	Number maxHealth;
	String mapName;
	OnEvent( Spawned )
	{
		id = entityId;
		health = maxHealth;
		FireEvent( EnemySpawned( mapName = mapName ) );
		FireEvent( GetNewWanderTargetPosition() );
	}

	Number newHealth;
	OnEvent( HealthUpdated )
	{
		health = newHealth;

		if( health == 1 )
		{
			FireEvent( ChangeSpriteAnimation( newAnim = "LowHealth" ) );
		}
	}

	OnEvent( Died )
	{
		FireEvent( EnemyDied( mapName = mapName, deadEntityId = id ) );
	}

	// TODO: Make this more intuitive
	Vec2 newPos;
	OnEvent( PositionUpdated )
	{
		lastPosition = position;
		position = newPos;
	}

	OnEvent( TargetPositionUpdated )
	{
		targetPosition = newPos;
	}
	
	// Damage
    Number attackDamage = 1;
	Number otherEntityId;
    OnEvent( CollisionEntered )
    {
        if( otherEntityId != -1 )
        {
			attackTargetId = otherEntityId;
            FireEvent( DealDamage() );
        }
    }

	OnEvent( CollisionExited )
    {
        attackTargetId = -1;
    }

	OnEvent( DealDamage )
	{
		if( attackTargetId != -1 )
		{
			FireEvent( DamageEntity( id = attackTargetId, damage = attackDamage ) );
			FireEvent( StartNewTimer( durationSeconds = .85, onCompletedEvent = "DealDamage" ) );
		}
	}

	// -----------------------------------------------------------------------------------------
	// Wander
	// -----------------------------------------------------------------------------------------
	State Wander
	{	
		OnEvent( TargetFound )
		{		
			ChangeState( Chase );
		}
				
		FireEvent( CheckForTarget( id = targetName, maxDist = 3 ) );
		FireEvent( MoveToLocation( pos = targetPosition ) );

		if( position == targetPosition )
		{
			FireEvent( GetNewWanderTargetPosition() );
		}

		// Check for stuck on wall, find new position if stuck for a second
		if( lastPosition == position )
		{
			numFramesInSameSpace = numFramesInSameSpace + 1;

			if( numFramesInSameSpace > 60 )
			{
				FireEvent( GetNewWanderTargetPosition() );
			}
		}
		else
		{
			numFramesInSameSpace = 0;
		}
	}

	// -----------------------------------------------------------------------------------------
	// Chase
	// -----------------------------------------------------------------------------------------
	State Chase
	{
		if( health == 1 )
		{
			ChangeState( Flee );
		}
		
		//FireEvent( PrintDebugText( text = "Chase" ) );

		Number distance;
		FireEvent( GetDistanceToTarget( id = targetName ) );
		OnEvent( DistanceToTargetUpdated )
		{		
			if( distance > 3 )
			{
				FireEvent( GetNewWanderTargetPosition() );
				ChangeState( Wander );
			}
		}

		FireEvent( ChaseTargetEntity( id = targetName ) );
	}

	// -----------------------------------------------------------------------------------------
	// Flee
	// -----------------------------------------------------------------------------------------
	State Flee
	{
		Number distance;
		OnEvent( DistanceToTargetUpdated )
		{		
			if( distance > 5 )
			{
				FireEvent( GetNewWanderTargetPosition() );
				ChangeState( Wander );
			}
		}

		//FireEvent( PrintDebugText( text = "Flee" ) );

		FireEvent( GetDistanceToTarget( id = targetName ) );

		FireEvent( FleeTargetEntity( id = targetName ) );
	}
}
