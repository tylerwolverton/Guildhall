StateMachine
{
    Number id;
    Number attackTargetId = -1;
    
    Number entityId;
    OnEvent( Spawned )
    {
        id = entityId;
    }

    State FollowPlayer
    {
        OnEnter()
        {
            attackTargetId = -1;
        }

        OnEvent( ItemThrown )
        {
            FireEvent( PrintDebugText( text = "Thrown", duration = 3 ) );
            ChangeState( Projectile );
        }

        FireEvent( ChaseTargetEntity( targetName = "player" ) );
    }

    State Projectile
    {
        OnEnter()
        {
            // If this plaque doesn't hit anything, return to player
            FireEvent( StartNewTimer( durationSeconds = 2, onCompletedEvent = "ReturnToPlayer" ) );
        }

        OnEvent( ReturnToPlayer )
        {
            FireEvent( AddItemToInventory( targetName = "player", itemId = id ) );
            ChangeState( FollowPlayer );
        }

        String otherEntityType;
        String otherEntityName;
        Number otherEntityId;
        OnEvent( CollisionEntered )
        {
            if( otherEntityId == -1 )
            {
                return;
            }

            if( otherEntityType != "Plaque" 
                && otherEntityName != "Player" )
            {
                attackTargetId = otherEntityId;
                ChangeState( Attacking );
            }
        }

        FireEvent( MoveInDirection( speed = 200, direction = Vec2( 0, 1 ) ) );
    }

    State Attacking
    {
        FireEvent( PrintDebugText( text = attackTargetId ) );
        OnEnter()
        {
            // If this plaque doesn't hit anything, return to player
            FireEvent( StartNewTimer( durationSeconds = 1, onCompletedEvent = "DealDamage" ) );
        }

        OnEvent( DealDamage )
        {
            if( attackTargetId != -1 )
            {
                FireEvent( DamageEntity( id = attackTargetId, damage = 1 ) );
                FireEvent( StartNewTimer( durationSeconds = 1, onCompletedEvent = "DealDamage" ) );
            }
        }

        Number deadEntityId;
        OnEvent( EnemyDied )
        {
            if( deadEntityId == attackTargetId )
            {
                ChangeState( FollowPlayer );
            }
        }

        FireEvent( ChaseTargetEntity( targetId = attackTargetId ) );
    }
}