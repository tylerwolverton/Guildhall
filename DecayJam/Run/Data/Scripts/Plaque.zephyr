StateMachine
{
    Number id;
    Number attackTargetId = -1;
    Vec2 moveDirection;
	Vec2 position;		
    Vec2 targetPosition;
    Number playerId;
    
    Number entityId;
    OnEvent( Spawned )
    {
        id = entityId;
    }

    Vec2 newPos;
	OnEvent( PositionUpdated )
	{
		position = newPos;
	}

    OnEvent( TargetPositionUpdated )
	{
		targetPosition = newPos;
	}

    State FollowPlayer
    {
        OnEnter()
        {
            attackTargetId = -1;
        }

        Vec2 direction;
        OnEvent( ItemThrown )
        {
            moveDirection = direction;
            ChangeState( Projectile );
        }

        FireEvent( ChaseTargetEntity( targetName = "player" ) );
    }

    State ReturnToPlayer
    {
        OnEnter()
        {
            attackTargetId = -1;
        }

        Number distance;
        OnEvent( DistanceToTargetUpdated )
		{		
			if( distance < .5 )
			{
                FireEvent( AddItemToInventory( targetName = "player", itemId = id ) );
				ChangeState( FollowPlayer );
			}
		}

		FireEvent( GetDistanceToTarget( id = "player" ) );
		
		FireEvent( ChaseTargetEntity( id = "player" ) );
    }

    State Projectile
    {
        OnEnter()
        {
            // If this plaque doesn't hit anything, return to player
            FireEvent( StartNewTimer( durationSeconds = 2, onCompletedEvent = "ReturnToPlayer" ) );
        }

        OnEvent( ReturnToPlayer )
        {
            ChangeState( ReturnToPlayer );
        }

        String otherEntityType;
        String otherEntityName;
        Number otherEntityId;
        OnEvent( CollisionEntered )
        {
            if( otherEntityId == -1 )
            {
                return;
            }

            if( otherEntityType != "Plaque" 
                && otherEntityName != "Player" )
            {
                attackTargetId = otherEntityId;
                ChangeState( Attacking );
            }
        }

        FireEvent( MoveInDirection( speed = 200, direction = moveDirection ) );
    }

    State Attacking
    {
        OnEnter()
        {
            // If this plaque doesn't hit anything, return to player
            FireEvent( StartNewTimer( durationSeconds = 1, onCompletedEvent = "DealDamage" ) );
        }

        OnEvent( DealDamage )
        {
            if( attackTargetId != -1 )
            {
                FireEvent( DamageEntity( id = attackTargetId, damage = 1 ) );
                FireEvent( StartNewTimer( durationSeconds = 1, onCompletedEvent = "DealDamage" ) );
            }
        }

        Number deadEntityId;
        OnEvent( EnemyDied )
        {
            if( deadEntityId == attackTargetId )
            {
                ChangeState( ReturnToPlayer );
            }
        }

        FireEvent( ChaseTargetEntity( targetId = attackTargetId ) );
    }
}