Entity target = null
Bool canAttack = true

Number breathCooldown = 5
Bool canBreatheFire = true

// -----------------------------------------------------------------------------------------
// Global events
// -----------------------------------------------------------------------------------------
Function Spawned( Number maxHealth, String mapName )
{
    AddAnimationEvent( anim: "Attack", frame: 8,      event: "DealDamage" )
    AddAnimationEvent( anim: "Attack", frame: "last", event: "ResetAttack" )
    AddAnimationEvent( anim: "Breath", frame: 19,     event: "ShootFire" )
    AddAnimationEvent( anim: "Breath", frame: "last", event: "ResetAttack" )
}


Function Died( String mapName )
{
	EnemyDied( mapName: mapName )
    PlaySound( soundName: "DragonDeath" )
	ChangeState( Dead )
}


Function OnWarp()
{
    moveTargetPosition = parentEntity.position
}


Function ResetAttack()
{
    canAttack = true
    ChangeState( Idle )
} 


Function DealDamage()
{
	if( !canAttack )
	{
		return
	}
	canAttack = false

	// Don't hit the player if they ran away
	Bool isOverlapping = false
	DoDiscsOverlap( center1: parentEntity.interactionCenter, radius1: parentEntity.interactionRadius, 
		center2: target.interactionCenter, radius2: target.interactionRadius, 
		isOverlapping: isOverlapping )
	if( !isOverlapping )
	{
		return
	}

    Number damage = parentEntity.attackDamage
    Bool isCrit = false
    CheckForCrit( isCrit: isCrit )

    if( isCrit )
    {
        damage = damage * 2
    }
	
	Vec2 printPosition = target.position + Vec2( 0, .75 );

	if( !isCrit )
	{
    	damage = damage - target.defense
	}

	if( damage <= 0 )
	{
		damage = 1
	}

    PrintDebugText( position: printPosition, text: damage, duration: .5, color: "red" )

    DamageEntity( target: target, damage: damage )
    AddScreenShake( intensity: .07 )
} 


// Function IsOverlappingTargetEntity( Entity parent, Entity target, Bool isOverlapping )
// {
// 	PrintDebugText( text: parent.interactionCenter )

//     DoDiscsOverlap( center1: parent.interactionCenter, radius1: parent.interactionRadius, 
//         center2: target.interactionCenter, radius2: target.interactionRadius, 
//         isOverlapping: isOverlapping )
// }


// -----------------------------------------------------------------------------------------
// Watch
// -----------------------------------------------------------------------------------------
State Idle
{		
	OnEnter()
	{
		ChangeSpriteAnimation( newAnim: "Idle" )
	}
	
	OnUpdate()
	{
		if( target.isDead )
		{
			return
		}

		Bool wasTargetFound = false
		CheckForTarget( target: target, maxDist: 10, targetFound: wasTargetFound )
		if( wasTargetFound )
		{
			ChangeState( Chase )
		}
	}	
}


// -----------------------------------------------------------------------------------------
// Chase
// -----------------------------------------------------------------------------------------
State Chase
{
	OnEnter()
	{
		ChangeSpriteAnimation( newAnim: "Walk" )
	}

	OnUpdate()
	{		
		if( canBreatheFire )
		{
			ChangeState( BreathAttack )
		}

		Bool isOverlapping = false
        DoDiscsOverlap( center1: parentEntity.interactionCenter, radius1: parentEntity.interactionRadius, 
			center2: target.interactionCenter, radius2: target.interactionRadius, 
			isOverlapping: isOverlapping )
        if( isOverlapping )
        {
            ChangeState( Attack )
        }

		MoveToLocation( pos: target.position )
	}
}


State Attack
{
    OnEnter()
    {
         PlaySpriteAnimation( newAnim: "Attack" )
		 PlaySound( soundName: "DragonAttack" )
    }    
}


Function ResetBreathWeapon()
{
	canBreatheFire = true
	AddScreenShake( intensity: .1 )
}


State BreathAttack
{
    OnEnter()
    {
		SpawnEntity( type: "Fireball", position: parentEntity.position + Vec2(0,1), direction: target.position - parentEntity.position, distance: 1 )
		// SpawnEntity( type: "Fireball", position: parentEntity.position + Vec2(0,1), direction: target.position - parentEntity.position, distance: 2 )
		// SpawnEntity( type: "Fireball", position: parentEntity.position + Vec2(0,1), direction: target.position - parentEntity.position, distance: 3 )

		 canBreatheFire = false;
         PlaySpriteAnimation( newAnim: "Breath" )
		 PlaySound( soundName: "DragonBreath", speed: 1.2 )
		 StartNewTimer( durationSeconds: breathCooldown, onCompletedEvent: "ResetBreathWeapon" )
    }    
}


State Dead
{
    OnEnter()
    {
        PlaySpriteAnimation( newAnim: "Death" )

		WinGame()
    }
}
